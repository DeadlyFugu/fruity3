
import files
import kiwi
import crayons
import repl
import builtin as _builtin

// detect if locking enabled
// we need it off to stub print/prompt for eval
// proper way to redirect io would be nice
{$root.print >root.print} catch {
  pop print(crayons.red! crayons.bold! 'error: please run with -l') exit
}
store: (:{
  items: list()
  pushBite: { name body =>
    kiwi.ctx_base + :{
      name: $name
      body: $body
      id: ($items len + 1)
    } items.push
  }
  pushChallenge: { name body =>
    items.pop + :{
      challengeName: $name
      challengeBody: $body
    } items.push
  }
  index: 'undefined'
} as this)

_asciiToSpan: list(
    _e: char(27)
    _s: {cat('<span class="' . '">')}
    cat($_e '[31m') _s! 'var'
    cat($_e '[32m') _s! 'string'
    cat($_e '[33m') _s! 'num'
    cat($_e '[34m') _s! 'sym'
    cat($_e '[35m') _s! 'keyword'
    cat($_e '[36m') _s! 'fn'
    cat($_e '[2m') _s! 'faint'
    cat($_e '[1m') _s! 'bold'
    cat($_e '[36m_USER_') cat(_s! 'faint' '&lt;your code here&gt;')
    cat($_e '[36m_TAB_</span> ') '    '
    cat($_e '[36m_NL_</span> ') '\n'
    cat(' ' $_e '[36m_NL_</span>') '\n'
    cat(' ' $_e '[36m_NL_</span> ') '\n'
    cat($_e '[0m') '</span>'
    '>' '&gt;'
    '<' '&lt;'
)
parseCode: { str .trim >>s
    f: nil
    clear(safe! {$s parse >f})
    $f then {
        crayons.rep($f) .sub(1 -1)
        ($_asciiToSpan open . fold {.replace})
    } else $s
}
parseVals: {
  map $crayons.rep join ' '
  ($_asciiToSpan open . fold {.replace})
}

eval_ctx: (:{import math} as root)
_eval: {
  parse $eval_ctx _builtin.evalin
}

simrepl: {
  reverse list >>ins
  until {$ins len = 0} do {
    ln: ins.pop
    cat('&gt; ' parseCode($ln))
    captured: list()
    {($_asciiToSpan open . fold {.replace}) captured.push} >_builtin.print
    {captured.push ins.empty then nil else {ins.pop dup cat('<em>' . '</em>\n') captured.push}} >root.prompt
    res: list({_eval($ln)} catch {dup .trace.pop pop repl.__pex})
    cat($captured open) .trim dup len = 0 then $pop
    len($res) > 0 then {(
      $res open
      parseVals
    )}
  }
}

// Build context for bites' kiwi templates
kiwi.ctx_html + (:{
  h: $kiwi.ctx_html.h3
  index: {>>store.index}
  bite: {s => s.splitfirst('\n') store.pushBite}
  challenge: {s => s.splitfirst('\n') store.pushChallenge}
  code: {s => cat('<code>' $s parseCode '</code>')}
  // answer: {s => cat('<div class="answer"><h2 class="answer-h" onclick="spoiler()">Answer</h2><div id="answer-spoiler" style="display:none">' $s '</div></div>')}
  answer: {s => cat('<h2>Answer</h2><div class="answer"><div class="answer-hide">' $s '</div><div class="answer-cover" onclick="spoiler(this)"><span>Reveal Answer</span></div></div>')}
  clrrepl: {s => (:{import math} as root) >eval_ctx}
  clrrepl2: {s => (:{} as root) >eval_ctx}
  repl: {s =>
    backupPrint: $_builtin.print
    backupPrompt: $root.prompt
    s.split('\n')
    under clear(. _eval)
    map {.trim}
    filter {len > 0}
    simrepl join '\n'
    cat('<pre>' . '</pre>')
    $backupPrint >_builtin.print
    $backupPrompt >root.prompt
  }
  // simple case
  case: {s =>
    s.splitfirst('\n') swap pop
    in: out: .splitfirst('\n')
    cat('&gt; ' parseCode($in) in.trim len != 0 then ' ' '<span class="comment">YOUR CODE HERE</span>')
    parseCode($out)
    join '\n'
    cat('<pre>' . '</pre>')
  }
  // program-should-print-something case
  pcase: {s =>
    s.splitfirst('\n') swap pop
    in: out: .splitfirst('\n')
    cat('&gt; ' parseCode($in))
    ($out .split('\n') map {.trim})
    join '\n'
    cat('<pre>' . '</pre>')
  }
  // function case
  fcase: {s =>
    s.splitfirst('\n') swap pop
    in: out: .split('\n')
    cat('&gt; ' parseCode($in))
    parseCode($out)
    join '\n'
    cat('<pre>' . '</pre>')
  }
  hint: {s =>
    cat('<p><em>' $s '</em></p>')
  }
  mono: {cat('<code>' . '</code>')}
  mono_block: {cat('<pre>' . '</pre>')}
  mono_bhl: {cat('<pre>' . parseCode '</pre>')}
} as this) >>bites_kiwi

bdir: files.path('./')

// Run main.kiwi to build up store.items
kiwi.run(bdir / 'main.kiwi' str $bites_kiwi)

files.mkdir(bdir / 'out' str)

// Save bites to files
($store.items open map { bite =>
  bdir / 'bite_template.kiwi' str
  bdir / cat('out/bite' $bite.id '.html') str
  $bite + (:{
    prev: ($bite.id > 1 then {
      cat('bite' $bite.id - 1 '.html')
    } else 'learn.html')
    next: ($bite.id < (store.items len) then {
      cat('bite' $bite.id + 1 '.html')
    } else 'learn.html')
  } as this)
  kiwi.parse_file
})

// Save learn.html
bdir / 'learn_template.kiwi' str
bdir / 'out/learn.html' str
kiwi.ctx_base + (:{
  index: $store.index
  forbites: {s => $store.items open map {item =>
    s.replace('{' '[') .replace('}' ']')
    $item kiwi.parse_string
  }}
} as this)
kiwi.parse_file

// copy style.css
files.read('style.css') files.write('out/style.css' .)
